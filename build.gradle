plugins {
    id 'java'
    id 'scala'
}

group = 'org.foton'

subprojects {
    repositories {
        mavenCentral()
        maven {
            name = 'papermc-repo'
            url = 'https://papermc.io/repo/repository/maven-public/'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public/'
        }
        maven {
            name = 'spigot-repo'
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        maven {
            name = 'CodeMC'
            url = 'https://repo.codemc.org/repository/maven-public/'
        }
        maven {
            name = 'lumine-repo'
            url = 'https://mvn.lumine.io/repository/maven-public/'
        }
        maven {
            url 'https://jitpack.io'
        }
        maven {
            url "https://repo.dmulloy2.net/repository/public/"
        }
        maven {
            url 'https://hub.spigotmc.org/nexus/content/repositories/public/'
        }
    }
}

// Добавьте сюда строку чтобы сконфигурировать подпроект, а чтобы зарегистрировать напишите его в settings.gradle
def tabSubprojects = ["Entities", "Discs", "WayStones"]

def libraries = ["Misc", "EntityRegistry", "DiscRegistry"]

// Билд, деплой в .server/plugins/, запуск сервера. (Ядро Airplane)
tasks.register("test-plugin", JavaExec) {
    it.dependsOn(":jar")
    it.workingDir = file("$rootDir/.server")
    it.classpath = files("$rootDir/.server/launcher-airplane.jar")
    it.jvmArgs("-javaagent:$rootDir/.server/launcher-airplane.jar", "-Xmx1G", "-Xms1G")
    // Закомментируйте чтобы включить GUI сервера.
    it.args("nogui")
    it.main("io.papermc.paperclip.Paperclip")
}

// Чистый запуск сервера без ребилда всех плагинов.
tasks.register("test-plugin-no-rebuild", JavaExec) {
    it.workingDir = file("$rootDir/.server")
    it.classpath = files("$rootDir/.server/launcher-airplane.jar")
    it.jvmArgs("-javaagent:$rootDir/.server/launcher-airplane.jar", "-Xmx1G", "-Xms1G")
    // Закомментируйте чтобы включить GUI сервера.
    it.args("nogui")
    it.main("io.papermc.paperclip.Paperclip")
}

// регистрирование подпроектов
tabSubprojects.forEach( pr -> project(":$pr") {
    apply plugin: 'java-library'
    apply plugin: 'scala'

    sourceSets {
        main {
            resources.srcDirs = ["$projectDir/main/resources"]
            resources.srcDirs = ["$projectDir/main/resources"]
            switch (pr) {
                case "Entities": {
                    scala.srcDirs = ["$projectDir/main/mixed"]
                    return
                }
                default: {
                    java.srcDirs = ["$projectDir/main/java"]
                    scala.srcDirs = ["$projectDir/main/scala"]
                    return
                }
            }
        }

        test {
            java.srcDirs = ["$projectDir/test/java"]
            scala.srcDirs = ["$projectDir/test/scala"]
            resources.srcDirs = ["$projectDir/test/resources"]
        }
    }

    dependencies {
        implementation project(':Libraries:Misc')
        implementation 'org.jetbrains:annotations:22.0.0'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
        testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.8.1'
        compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'

        // Scala
        compileOnly group: 'org.scala-lang', name: 'scala3-library_3', version: '3.1.0'
        switch (pr) {
            case "Entities": {
                compileOnly 'com.ticxo.modelengine:api:R2.2.0'
                implementation project(":Libraries:EntityRegistry")
                implementation group: 'tf.tofu', name: 'tofu-core_2.13', version: '0.10.3'

                // Tests
                testImplementation 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
                return
            }
            case "Discs": {
                implementation project(":Libraries:DiscRegistry")
                return
            }
            case "WayStones": {
                compileOnly 'com.github.LoneDev6:api-itemsadder:2.4.21'
                compileOnly 'com.comphenix.protocol:ProtocolLib:4.7.0'
                implementation 'net.wesjd:anvilgui:1.5.3-SNAPSHOT'
                return
            }
            default: return
        }
    }

    // Костыль для билда скалы с джавой вместе в разных папках.
    //tasks.compileJava.classpath += files(sourceSets.main.scala.classesDirectory)
    //tasks.compileScala.classpath = sourceSets.main.compileClasspath

    jar {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        from {
            configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        }
        destinationDir = file("$rootDir/.server/plugins")
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
    }
})
project(":Libraries") {
    apply plugin: 'java-library'
}
// Регистрация проектовых библиотек
libraries.forEach(lib -> project(":Libraries:$lib") {
    apply plugin: 'java-library'
    apply plugin: 'scala'

    sourceSets {
        main {
            resources.srcDirs = ["$projectDir/main/resources"]
            switch (lib) {
                case "Misc": {
                    scala.srcDirs = ["$projectDir/main/mixed"]
                    return
                }
                case "EntityRegistry": {
                    scala.srcDirs = ["$projectDir/main/mixed"]
                    return
                }
                default: {
                    java.srcDirs = ["$projectDir/main/java"]
                    scala.srcDirs = ["$projectDir/main/scala"]
                    return
                }
            }
        }

        test {
            java.srcDirs = ["$projectDir/test/java"]
            scala.srcDirs = ["$projectDir/test/scala"]
            resources.srcDirs = ["$projectDir/test/resources"]
        }
    }

    dependencies {
        implementation 'org.jetbrains:annotations:22.0.0'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
        testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.8.1'
        compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'

        // Scala
        compileOnly group: 'org.scala-lang', name: 'scala3-library_3', version: '3.1.0'
        testImplementation group: 'org.scala-lang', name: 'scala3-library_3', version: '3.1.0'
        switch (lib) {
            case "Misc": {
                compileOnly files("$rootDir/.sources/patched_1.17.1.jar")
                return
            }
            case "EntityRegistry": {
                compileOnly project(':Libraries:Misc')
                // импортирует nms прямо из папируса, конечно так публиковать джарник нелегально, но у нас приватый репо, нам можно.
                compileOnly files("$rootDir/.sources/patched_1.17.1.jar")
                compileOnly group: 'io.projectreactor', name: 'reactor-core', version: '3.4.12' // реактивность для удобства кодинга ИИ.
                compileOnly 'com.ticxo.modelengine:api:R2.2.0'
                //compileOnly group: 'de.tr7zw', name: 'item-nbt-api-plugin', version: '2.8.0'

                // Скаловская библиотека от разрабов тинькофф банка для очень удобного ФП с Cats/ZIO.
                implementation group: 'tf.tofu', name: 'tofu-core_2.13', version: '0.10.3'

                // Tests
                testImplementation 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
                testImplementation group: 'io.projectreactor', name: 'reactor-core', version: '3.4.12'
                testImplementation project(':Libraries:Misc')
                return
            }
            case "DiscRegistry": {
                compileOnly project(':Libraries:Misc')
                compileOnly files("$rootDir/.sources/patched_1.17.1.jar")
                return
            }
            default: return
        }
    }

    // Костыль для билда скалы с джавой вместе в разных папках.
    //tasks.compileJava.classpath += files(sourceSets.main.scala.classesDirectory)
    //tasks.compileScala.classpath = sourceSets.main.compileClasspath

    jar {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
    }
})

// Отдельный плагин, который просто в себе хранит всю скаловскую стандартную библиотеку,
// лучше найти другой способ для использования скалы на сервере.
project(":Libraries:ScalaSupport") {
    apply plugin: 'java-library'
    apply plugin: 'scala'

    sourceSets {
        main {
            scala.srcDirs = ["$projectDir/main/scala"]
            resources.srcDirs = ["$projectDir/main/resources"]
        }

        test {
            scala.srcDirs = ["$projectDir/test/scala"]
            resources.srcDirs = ["$projectDir/test/resources"]
        }
    }

    dependencies {
        compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
        implementation group: 'org.scala-lang', name: 'scala3-library_3', version: '3.1.0'
    }

    jar {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        from {
            configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        }
        destinationDir = file("$rootDir/.server/plugins")
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
    }
}

jar {
    dependsOn(project(":Libraries:ScalaSupport").tasks.buildNeeded)
    dependsOn(project(":Libraries:ScalaSupport").tasks.clean)
    dependsOn(project(":Libraries").tasks.buildNeeded)
    tabSubprojects.forEach( pr -> {
        dependsOn(project(":$pr").tasks.buildNeeded)
        dependsOn(project(":$pr").tasks.clean)
    })
}

// Штучки от бумаги, не трогать!
def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}